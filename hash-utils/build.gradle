//import net.ltgt.gradle.errorprone.ErrorProneOptions
//import net.ltgt.gradle.nullaway.NullAwayOptions

// TODO DELETE ME - i am triggering actions
// TODO DELETE ME - i am triggering actions

plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.1.1'
    id 'net.minecraftforge.gradleutils' version '2.4.2'
}

repositories {
    maven gradleutils.forgeMaven
    mavenCentral()
    mavenLocal()
}

group = 'net.minecraftforge'
version = gitversion.version.tagOffset
println('Version: ' + version)

apply from: rootProject.file('build_shared.gradle')

dependencies {
    // Static Analysis
    compileOnly libs.nulls
}

java {
    // Java 8, because I know super ancient shit might need this (like Java Version!)
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

jar {
    manifest {
        attributes([
            'Specification-Title':   'Hash Utils',
            'Specification-Vendor':  'Forge Development LLC',
            'Specification-Version':  gitversion.version.info.tag,
            'Implementation-Title':  'Hash Utils',
            'Implementation-Vendor': 'Forge Development LLC',
            'Implementation-Version': project.version
        ] as LinkedHashMap, 'net/minecraftforge/util/hash/')
    }
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        artifactId = project.name
        from components.java

        pom { pom ->
            name = 'Hash Utils'
            description = 'Common utilities for working with file hashes and caches'

            gradleutils.pom.gitHubDetails = pom

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
                developer gradleutils.pom.Developers.Jonathing
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}
